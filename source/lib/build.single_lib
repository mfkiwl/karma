#! /bin/csh -f
#
#   This C-shell script will build a master directory for a library which
#   contains symbolic links to the source code.
#
#   Copyright (C) 1992,1993  Richard Gooch
#
#   This program is free software; you can redistribute it and/or modify
#   it under the terms of the GNU General Public License as published by
#   the Free Software Foundation; either version 2 of the License, or
#   (at your option) any later version.
#
#   This program is distributed in the hope that it will be useful,
#   but WITHOUT ANY WARRANTY; without even the implied warranty of
#   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#   GNU General Public License for more details.
#
#   You should have received a copy of the GNU General Public License
#   along with this program; if not, write to the Free Software
#   Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.
#
#   Richard Gooch may be reached by email at  karma-request@atnf.csiro.au
#   The postal address is:
#     Richard Gooch, c/o ATNF, P. O. Box 76, Epping, N.S.W., 2121, Australia.

rm -f *~ >& /dev/null
rm -f */*~ >& /dev/null
rm -f */*/*~ >& /dev/null

foreach file (master/*.c)
    if ( ! -r $file ) rm $file
end

set arches = (SUNsparc convex rs6000 dec VXMVX linux)

cd packages
foreach package (*)
    cd $package
    foreach file (*.c)
	# See if file includes its prototypes
	if ( (`fgrep -c "karma_${package}.h" $file` < "1") && ("$file" != "globals.c") ) then
	    echo "$package/$file    WARNING: prototype file not included"
	endif
	if ( ! -r ../../master/${package}_$file) then
	    echo "$package/$file"
	    ln -s ../packages/$package/$file ../../master/${package}_$file
	endif
    end
    foreach arch ($arches)
	if (-d $arch) then
	    foreach file (../../master/$arch/*.s)
		if ( ! -r $file ) rm $file
	    end
	    cd $arch
	    foreach file (*.s)
		if ( ! -r ../../../master/$arch/${package}_asm_$file) then
		    echo "$package/$arch/$file"
		    ln -s ../../packages/$package/$arch/$file ../../../master/$arch/${package}_asm_$file
		endif
	    end
	    cd ..
	endif
    end
    cd ..
end
