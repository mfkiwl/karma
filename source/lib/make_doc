#! /bin/csh -f
#
#   This C-shell script will build the documentation for the libraries.
#
#   Copyright (C) 1992,1993,1994,1995  Richard Gooch
#
#   This program is free software; you can redistribute it and/or modify
#   it under the terms of the GNU General Public License as published by
#   the Free Software Foundation; either version 2 of the License, or
#   (at your option) any later version.
#
#   This program is distributed in the hope that it will be useful,
#   but WITHOUT ANY WARRANTY; without even the implied warranty of
#   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#   GNU General Public License for more details.
#
#   You should have received a copy of the GNU General Public License
#   along with this program; if not, write to the Free Software
#   Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.
#
#   Richard Gooch may be reached by email at  karma-request@atnf.csiro.au
#   The postal address is:
#     Richard Gooch, c/o ATNF, P. O. Box 76, Epping, N.S.W., 2121, Australia.

rm -f *~ >& /dev/null

set docdir = $KARMABASE/doc/lib
set tmpfile = /tmp/$$
rm -f $docdir/*~ >& /dev/null
cp -p $docdir/README.header $docdir/README
rm -f $tmpfile
set tmp = `date`
set date = "${tmp[3]}-${tmp[2]}-${tmp[$#tmp]}"
set unchanged = "TRUE"

foreach lib (karma*)
    echo "Processing library: $lib"
    if ( ! -d $lib/packages ) continue
    cd $lib/packages
    foreach package (`find_changed_packages`)
	set docfile = $docdir/${package}.doc
	set listfile = $docdir/package_functions/$package
	set liblinefile = $docdir/liblines/${package}
	rm -f $docfile $listfile
	if ( ! -e ${package}/doc-header) then
	    echo "Header file for  ${package}  package does not exist: WRITE IT"
	    continue
	endif
	set unchanged = "FALSE"
	foreach i ($package/*.c)
	    echo $i
	    if ( ! -e $docfile ) then
		# Create doc file
		echo "/*  Link with:    -l$lib  */" > $docfile
		tail +2 ${package}/doc-header | sed -e "s/<<DATE>>/${date}/" >> $docfile
		head -1 ${package}/doc-header > $liblinefile
		echo "/*  Link with:    -l$lib  */" >> $liblinefile
	    endif
	    c2doc $i >> $docfile
	    extract_functions $i >> $listfile
	end
    end
    cd ../..
end

if ("$unchanged" == "TRUE") then
    exit 0
endif

echo ""
echo "Sorting packages..."
cd $docdir/package_functions
foreach package (`sort_packages`)
    echo "$package"
    echo "" >> $docdir/README
    echo "-----------------------------------------------------------------------------" >> $docdir/README
    cat $docdir/liblines/${package} >> $docdir/README
    echo "" >> $docdir/README
    set tmp = `cat $package`
    @ num_lines = $#tmp
    @ newlines = $num_lines / 2 + $num_lines % 2
    pr -t -2 -l$newlines $package >> $docdir/README
end
