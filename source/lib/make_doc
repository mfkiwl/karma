#! /bin/csh -f
#
#   This C-shell script will build the documentation for the libraries.
#
#   Copyright (C) 1992,1993  Richard Gooch
#
#   This program is free software; you can redistribute it and/or modify
#   it under the terms of the GNU General Public License as published by
#   the Free Software Foundation; either version 2 of the License, or
#   (at your option) any later version.
#
#   This program is distributed in the hope that it will be useful,
#   but WITHOUT ANY WARRANTY; without even the implied warranty of
#   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#   GNU General Public License for more details.
#
#   You should have received a copy of the GNU General Public License
#   along with this program; if not, write to the Free Software
#   Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.
#
#   Richard Gooch may be reached by email at  karma-request@atnf.csiro.au
#   The postal address is:
#     Richard Gooch, c/o ATNF, P. O. Box 76, Epping, N.S.W., 2121, Australia.

rm -f *~ >& /dev/null

set docdir = $KARMABASE/doc/lib
set tmpfile = /tmp/$$
rm -f $docdir/*~ $docdir/list_build/* $docdir/*.doc >& /dev/null
cp -p $docdir/headers/README.header $docdir/README
rm -f $tmpfile

foreach lib (karma*)
    echo "Processing library: $lib"
    cd $lib
    foreach i (level*/*.c)
	set prefix = `get_prefix $i`
	if ("$status" == "0") then
	    echo $i
	    set docfile = $docdir/${prefix}.doc
	    set listfile = $docdir/list_build/$prefix
	    set liblinefile = $docdir/headers/${prefix}.lib
	    if ( ! -e $docdir/headers/${prefix}.head) then
		echo "Header file for:    $docfile    does not exist: WRITE IT"
		exit 1
	    endif
	    if ( ! -e $docfile ) then
		# Create doc file
		echo "/*  Link with:    -l$lib  */" > $docfile
		tail +2 $docdir/headers/${prefix}.head >> $docfile
		echo "/*  Link with:    -l$lib  */" > $liblinefile
	    endif
	    c2doc $i >> $docfile
	    extract_functions $i >> $listfile
	endif
    end
    cd ..
end

cd $docdir/list_build
foreach prefix (`sort_packages`)
    echo "" >> ../README
    echo "-----------------------------------------------------------------------------" >> ../README
    head -1 $docdir/headers/${prefix}.head >> ../README
    cat $docdir/headers/${prefix}.lib >> ../README
    echo "" >> ../README
    set tmp = `cat $prefix`
    @ num_lines = $#tmp
    @ newlines = $num_lines / 2 + $num_lines % 2
    pr -t -2 -l$newlines $prefix >> ../README
end

rm -f $docdir/headers/*.lib $docdir/list_build/* >& /dev/null
