#! /bin/csh -f

#   This C shell script will create the requisite symbolic links in the
#   $KARMALIBPATH  directory, needed for the shared library implementations
#   on some platforms.

#   Copyright (C) 1994  Richard Gooch
#
#   This program is free software; you can redistribute it and/or modify
#   it under the terms of the GNU General Public License as published by
#   the Free Software Foundation; either version 2 of the License, or
#   (at your option) any later version.
#
#   This program is distributed in the hope that it will be useful,
#   but WITHOUT ANY WARRANTY; without even the implied warranty of
#   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#   GNU General Public License for more details.
#
#   You should have received a copy of the GNU General Public License
#   along with this program; if not, write to the Free Software
#   Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.
#
#   Richard Gooch may be reached by email at  karma-request@atnf.csiro.au
#   The postal address is:
#     Richard Gooch, c/o ATNF, P. O. Box 76, Epping, N.S.W., 2121, Australia.

switch ("$OS")
    case "SunOS":
    case "Linux":
    case "Solaris":
	# Machines which support library version numbers go above
	breaksw
    case "IRIX5":
    case "OSF1":
    case "HPUX":
    case "ConvexOS":
    case "ULTRIX":
    case "AIX":
    case "VXMVX":
	# Machines which don't support library version numbers go above
	exit 0
	breaksw
    default:
	# Default is library version numbers supported
	breaksw
endsw

@ karma_version = `echo $KARMA_VERSION | cut -d. -f1`

cd $KARMALIBPATH

foreach lib (karma*)
    if ("$OS" == "Solaris") then
	rm lib${lib}.so
	ln -s lib${lib}.so.$KARMA_VERSION lib${lib}.so
	echo ln -s lib${lib}.so.$KARMA_VERSION lib${lib}.so
    endif
end

foreach lib (karma*)
    if ("$OS" == "Linux") then
	if ( ! -f lib${lib}.so.$karma_version ) then
	    rm lib${lib}.so.$karma_version
	    ln -s lib${lib}.so.$KARMA_VERSION lib${lib}.so.$karma_version
	    echo ln -s lib${lib}.so.$KARMA_VERSION lib${lib}.so.$karma_version
	    set mkimage_flags = -f
	endif
    endif
end

if ("$OS" == "Linux") then
    if ("$?mkimage_flags" == "0") then
	cp /dev/null mkimage_flags
    else
	echo "$mkimage_flags" > mkimage_flags
    endif
endif

foreach lib (karma*)
    foreach file (lib${lib}.so.*.*)
	if ("$file" != "lib${lib}.so.$KARMA_VERSION") then
	    echo "Removing: $file"
	    rm $file
	endif
    end
    if ("$OS" == "SunOS") then
	foreach file (lib${lib}.sa.*.*)
	    if ("$file" != "lib${lib}.sa.$KARMA_VERSION") then
		echo "Removing: $file"
		rm $file
	    endif
	end
    endif
end

exit 0
