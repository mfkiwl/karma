#! /bin/echo Must be sourced, not executed

# usage: switch-karma  production|experimental

if ("$#argv" != "1") then
    echo "Usage: switch-karma  production|experimental"
    exit 1
endif

if ("$?HOSTNAME" == "0") then
    # Find out hostname: so many ways...
    if (-x /bin/uname) then
	setenv HOSTNAME `/bin/uname -n`
    else if (-x /usr/bin/uname) then
	setenv HOSTNAME `/usr/bin/uname -n`
    else if (-x /bin/hostname) then
	setenv HOSTNAME `/bin/hostname`
    else if (-x /usr/bin/hostname) then
	setenv HOSTNAME `/usr/bin/hostname`
    else
	echo "I don't know how to find out your hostname. Not another way!"
	exit 1
    endif
endif

if ("$argv[1]" == "production") then
    if ( ! -d /usr/local/karma ) then
	echo "Production version of sparc_Solaris Karma not available"
	exit 2
    endif
    switch ("$HOSTNAME")
	case "phoenix":
	    set oldroot = /phoenix/karma
	    set oldbase = ${oldroot}/${MACHINE_OS}
	    set newroot = /applic/karma
	    set newbase = /usr/local/karma
	    echo "Using production version of ${MACHINE_OS} Karma"
	    breaksw
	case "raptor":
	    set oldroot = /usr/llocal/karma-exp
	    set oldbase = ${oldroot}/${MACHINE_OS}
	    set newroot = /applic/karma
	    set newbase = /usr/local/karma
	    echo "Using production version of ${MACHINE_OS} Karma"
	    breaksw
	default:
	    exit 0
	    breaksw
    endsw
endif

if ("$argv[1]" == "experimental") then
    switch ("$HOSTNAME")
	case "phoenix":
	    set oldroot = /applic/karma
	    set oldbase = /usr/local/karma
	    set newroot = /phoenix/karma
	    set newbase = ${newroot}/${MACHINE_OS}
	    if ( ! -d $newbase ) then
		echo "Experimental version of $MACHINE_OS Karma not available"
		exit 2
	    endif
	    echo "Using experimental version of ${MACHINE_OS} Karma"
	    breaksw
	case "raptor":
	    set oldroot = /applic/karma
	    set oldbase = /usr/local/karma
	    set newroot = /usr/llocal/karma-exp
	    set newbase = ${newroot}/${MACHINE_OS}
	    if ( ! -d $newbase ) then
		echo "Experimental version of $MACHINE_OS Karma not available"
		exit 2
	    endif
	    echo "Using experimental version of ${MACHINE_OS} Karma"
	    breaksw
	default:
	    exit 0
	    breaksw
    endsw
endif

setenv KARMABASE $newbase
setenv KARMABINPATH $KARMABASE/bin
setenv KARMALIBPATH $KARMABASE/lib
setenv KARMAINCLUDEPATH $KARMABASE/include
setenv KARMAROOT $newroot
setenv PATH `echo "$PATH" | sed -e "s*${oldbase}*${newbase}*g" | sed -e "s*${oldroot}*${newroot}*g"`
set _cdpath = ()
foreach i ($cdpath)
    if ("$i" == "$oldroot") then
	set _cdpath = ($_cdpath $newroot)
    else
	set _cdpath = ($_cdpath $i)
    endif
end
set cdpath = ($_cdpath)
unset _cdpath

# Set version number environment variable
set _versionfile = $KARMAINCLUDEPATH/k_version.h
set tmp = `fgrep KARMA_VERSION $_versionfile | tr '"' ' '`
setenv KARMA_VERSION "$tmp[$#tmp]"
if ("$KARMA_VERSION" == "") then
    echo "WARNING: KARMA_VERSION environment variable could not be computed."
endif
unset _versionfile
